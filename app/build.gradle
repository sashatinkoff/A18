apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

apply from: '../tasks.gradle'

def versionPropsFile = file('version.properties')
android {
    compileSdkVersion versions.compileSdk

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.isidroid.a18"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0
        def versionBuild = 0

        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger()
        }

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", applicationId + "." + versionName)
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("ISIDROID_KEYSTORE"))
            storePassword System.getenv("ISIDROID_KEYSTORE_PASSWORD") ?: 'UNSET'
            keyAlias System.getenv("ISIDROID_KEY_ALIAS") ?: 'UNSET'
            keyPassword System.getenv("ISIDROID_KEY_PASSWORD") ?: 'UNSET'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        preDexLibraries = true
        incremental true
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin"

    testImplementation "junit:junit:${versions.junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso_core}"
// support
    implementation "androidx.core:core-ktx:${versions.corektx}"
    implementation "androidx.appcompat:appcompat:${versions.androidx}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraint}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "android.arch.work:work-runtime:${versions.worker}"
    implementation "com.google.android.gms:play-services-location:${versions.playServicesLocation}"
// livedata
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-extensions:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-runtime:${versions.lifecycle}"
    kapt "androidx.lifecycle:lifecycle-compiler:${versions.lifecycle}"

// rx
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxJava}"
// di
//    implementation "com.google.dagger:dagger-android:${versions.dagger}"
//    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
//    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"
// Retrofit
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
// Glide
    implementation("com.github.bumptech.glide:glide:${versions.glide}") {
        exclude group: "com.android.support"
    }
    kapt "com.github.bumptech.glide:compiler:${versions.glide}"
// Stuff
    implementation "com.google.code.gson:gson:${versions.gson}"
    implementation "com.karumi:dexter:${versions.dexter}"

    implementation 'com.google.zxing:core:3.3.0'

// cores
    implementation project(':utils')
    implementation project(':logger')
    implementation project(':realm')
    implementation project(':pics')

    // include modules as aars
//    implementation(name: "logger.${versions.isidroid}", ext: 'aar')
//    implementation(name: "utils.${versions.isidroid}", ext: 'aar')
//    implementation(name: "realm.${versions.isidroid}", ext: 'aar')
//    implementation(name: "pics.${versions.isidroid}", ext: 'aar')
}

//apply plugin: 'com.google.gms.google-services'

